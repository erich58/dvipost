dnl Process this file with autoconf to produce a configure script.
AC_INIT(dvipost.c)

dnl Checks for programs.
AC_PROG_CC

if	test "$GCC" = yes
then	CFLAGS="$CFLAGS -Wall -Wmissing-prototypes"
fi

AC_PROG_LN_S

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strtod)

dnl Determine LaTeX install directory

AC_MSG_CHECKING(latex environment)

if	texpath=`kpsewhich latex.ltx 2>/dev/null`
then	kpseflag=""
elif	texpath=`kpsewhich tex latex.ltx 2>/dev/null`
then	kpseflag="-DKPSEWHICH_NEED_TYPE"
else	texpath="."; kpseflag=""
fi

changequote(, )
texpath=`echo $texpath | sed -e 's%/[^/][^/]*/[^/]*$%/misc%'`
changequote([,])

if	test "$texpath" = "."
then	AC_MSG_RESULT("broken")
elif	test "$kpseflag" != ""
then	AC_MSG_RESULT(kpsewhich needs type)
else	AC_MSG_RESULT(ok)
fi

AC_SUBST(kpseflag)dnl
AC_SUBST(texpath)dnl

AC_OUTPUT(Makefile)
