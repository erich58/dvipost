dnl Process this file with autoconf to produce a configure script.
AC_INIT(dvipost.c)

dnl Checks for programs.
AC_PROG_CC

dnl Add warning flags for gcc

if	test "$GCC" = yes
then	CFLAGS="$CFLAGS -Wall -Wmissing-prototypes"
fi

AC_PROG_INSTALL
AC_PROG_LN_S

dnl Determine LaTeX install directory

AC_MSG_CHECKING(latex environment)

if	texpath=`kpsewhich latex.ltx 2>/dev/null`
then	kpseflag=""
elif	texpath=`kpsewhich tex latex.ltx 2>/dev/null`
then	kpseflag="-DKPSEWHICH_NEED_TYPE"
else	texpath="."; kpseflag=""
fi

changequote(, )
texpath=`echo $texpath | sed -e 's%/[^/][^/]*/[^/]*$%/misc%'`
changequote([,])

if	test "$texpath" = "."
then	AC_MSG_RESULT("broken")
elif	test "$kpseflag" != ""
then	AC_MSG_RESULT(kpsewhich needs type)
else	AC_MSG_RESULT(ok)
fi

AC_SUBST(kpseflag)
AC_SUBST(texpath)

AC_MSG_CHECKING(if latex accepts options)
if	latex -version < /dev/null > /dev/null
then	texflag=""
else	texflag="-DXXX"
fi

if	test "$texflag" = ""
then	AC_MSG_RESULT("yes")
else	AC_MSG_RESULT(no)
fi

AC_SUBST(texflag)
AC_OUTPUT(Makefile)
